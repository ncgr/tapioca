#! /usr/bin/env perl

# Tapioca
# Copyright (C) 2013 National Center for Genome Resources - http://ncgr.org
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License along
# with this program; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
#

use strict;
use Getopt::Long;
use File::Path;
use File::Spec;
use IO::File;
use Bio::SeqReader::Fastq;

my $_I_AM    = 'tap_splitq';
my $_VERSION = '%BUNDLE_VERSION%';

my $help_flag;
my $outname;
my $verbosity = 0;
my $version_flag;

Getopt::Long::Configure( 'no_ignore_case', 'no_auto_abbrev' );
Getopt::Long::GetOptions(
    'help|h'     => \$help_flag,
    'out|o=s'    => \$outname,
    'verbose|V+' => \$verbosity,
    'version|v'  => \$version_flag,
);

if ( $version_flag ) {
    print "$_I_AM $_VERSION\n";
    exit 0;
}

if ( $help_flag ) {
    print <<_TEXT;
Usage: $_I_AM [OPTIONS] <id_file1> ...
Splits a set of reads in fastq format into two sets based upon the labels in
<id_file>, ... Each id_file contains single line records with sequence
identifiers to segregate as described below. Repeated records are ok.

Options:
 -h, --help
       Print this message and exit; specify --verbose for more.
 -o, --out <name>
       Specify <name> as the basename to be used for the results.  Produces
       output files <name>__a.fq and <name>__b.fq. For example,
       "--out /tmp/foo/abc123" will result in the files /tmp/foo/abc123__a.fq
       and /tmp/foo/abc123__b.fq. 
 -V, --verbose
       Increase the level of verbosity; multiples increase output.
 -v, --version
       Print the version information and exit.
_TEXT

    if ( $verbosity > 0 ) {
        print <<_TEXT;

$_I_AM is a great little program ...
_TEXT
    }

    exit 0;
}


my ( $v, $targetdir, $targetname ) = File::Spec->splitpath( $outname );

print "Creating $targetdir\n" if $verbosity > 0;
File::Path::make_path( $targetdir );

my %flagged_ids;

while ( @ARGV ) {
    my $id_file = shift @ARGV;

    if ( $id_file && -f $id_file ) {    # read the flagged ids if specified

        open IN, $id_file;

        while ( <IN> ) {
            chomp;
            next if /^\s*$/;
            next if /^\s*#/;
            s/^\s+//g;                  # remove leading whitespace
            my @vals = split( /\s+/, $_ );

            $flagged_ids{ $vals[0] } = 1;
        }

        close IN;
    }
}

### Split the input stream into two files

my $in = new Bio::SeqReader::Fastq();    # stdin

open OUT1, ">" . File::Spec->catfile( $targetdir, $targetname . "__a.fq" );
open OUT2, ">" . File::Spec->catfile( $targetdir, $targetname . "__b.fq" );

while ( my $so = $in->next() ) {
    my $id = $so->display_id();
    my $h1 = $so->header1();
    my $h2 = $so->header2();
    my $s  = $so->seq();
    my $q  = $so->quals();

    exists $flagged_ids{ $id } ? print OUT1 "\@$h1\n$s\n\+$h2\n$q\n" : print OUT2 "\@$h1\n$s\n\+$h2\n$q\n";
}

close OUT1;
close OUT2;
